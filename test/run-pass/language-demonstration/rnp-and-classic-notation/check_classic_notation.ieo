export (check_classic_notation)

import simpleparsing:find_outside

# check if expressions in classic notation made with (,),+,*,a,b,c are correct
fun check_classic_notation(s) { Expression(s) }

# Expression = Component "+" Expression | Component
fun Expression(s) {
  if (p = find_outside("+", "(", ")", s)) {
    Component(cut(0, p, s)) and Expression(cut(p + 1, 0, s))
  } else {
    Component(s)
  }
}

# Component = Factor "*" Component | Factor
fun Component(s) {
  if (p = find_outside("*", "(", ")", s)) {
    Factor(cut(0, p, s)) and Component(cut(p + 1, 0, s))
  } else {
    Factor(s)
  }
}

# Factor = Identity | "(" Expression ")"
fun Factor(s) {
  Identity(s) or (head(s) == "(" and last(s) == ")" and Expression(cut(1, -1, s)))
}

# Identity = "a" | "b" | "c"
fun Identity(s) {
	s == "a" or s == "b" or s == "c"
}
